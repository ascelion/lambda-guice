plugins {
	id 'build.java-library'
	id 'build.java-test-fixtures'
}

sourceSets {
	jupiter
	testng
}

java {
	registerFeature 'jupiter', {
		usingSourceSet sourceSets.jupiter
		withSourcesJar()
	}
	registerFeature 'testng', {
		usingSourceSet sourceSets.testng
		withSourcesJar()
	}
}

configurations {
	jupiterImplementation.extendsFrom testFixturesImplementation
	testngImplementation.extendsFrom testFixturesImplementation

	jupiterTestImplementation.extendsFrom jupiterImplementation
	testngTestImplementation.extendsFrom testngImplementation
}

dependencies {
	api libs.guice
	api libs.jakarta.annotation
	api libs.jakarta.cdi
	api libs.jakarta.inject

	implementation libs.bytebuddy
	implementation libs.classgraph
	implementation libs.commons.lang3

	testFixturesApi tests.mockito.core
	testFixturesImplementation libs.commons.lang3
	testFixturesImplementation tests.system.stubs.core

	jupiterApi platform(tests.jupiter.bom)
	jupiterApi tests.jupiter.api
	jupiterApi tests.mockito.jupiter
	jupiterApi tests.system.stubs.jupiter
	jupiterImplementation sourceSets.testFixtures.output

	testngApi platform(tests.jupiter.bom)
	testngApi tests.jupiter.api
	testngApi tests.testng
	testngApi tests.mockito.testng
	testngApi tests.system.stubs.testng
	testngImplementation sourceSets.testFixtures.output
}

testing {
	suites {
		jupiterTest {
			useJUnitJupiter tests.jupiter.bom.get().version

			dependencies {
				implementation project(), {
					capabilities {
						requireCapability("${project.group}:boot-jupiter")
					}
				}
			}
		}
		
		testngTest {
			useTestNG tests.testng.get().version

			dependencies {
				implementation project(), {
					capabilities {
						requireCapability("${project.group}:boot-testng")
					}
				}
			}
		}
	}
}
