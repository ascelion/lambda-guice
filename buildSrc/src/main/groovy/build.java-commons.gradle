import org.gradle.api.tasks.compile.AbstractCompile
import org.gradle.api.tasks.compile.JavaCompile
import org.gradle.jvm.toolchain.JavaLanguageVersion

plugins {
	id 'java'

	id 'build.constraints'

	id 'build.java-tests'
	id 'build.java-eclipse'

	id 'io.freefair.lombok'
}

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(javaVersion)
	}
	consistentResolution {
		useCompileClasspathVersions()
	}

	withSourcesJar()
}

lombok {
	version = libs.lombok.get().version
}

dependencies {
	deps libs.slf4j.api

	deps libs.commons.io
	deps libs.commons.lang3
	deps libs.commons.collections4
}

def compileAllTask = tasks.register('compileAll')

tasks.withType AbstractCompile, { task ->
	compileAllTask.configure {
		dependsOn task
	}
}

tasks.withType JavaCompile, {
	options.compilerArgs << '-parameters'
}

repositories {
	mavenCentral()
}
